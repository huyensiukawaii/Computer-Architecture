.eqv KEY_CODE 0xFFFF0004  
.eqv KEY_READY 0xFFFF0000
.eqv YELLOW 0x00FFFF00                          		
.eqv SCREEN_MONITOR 0x10010000 

.data
circle_end:	.word	1	# Con trỏ trỏ đến cuối của chu kỳ
circle:		.word		# Con trỏ trỏ đến mảng lưu dữ liệu của chu kỳ
.text
initial_declare:
	addi	$s0, $0, 255	# x = 255
	addi	$s1, $0, 255	# y = 255
	addi	$s2, $0, 0	# dx = 0
	add	$s3, $0, $0	# dy = 0
	addi	$s4, $0, 20	# r = 20
	addi	$a0, $0, 50	# t = 50ms/frame
	jal	circle_data

input:		
	li	$k0, KEY_READY	# Kiểm tra xem có dữ liệu nhập không
	lw	$t0, 0($k0)
	bne	$t0, 1, edge_check
	jal	direction_change

# Kiểm tra xem hình tròn có chạm vào cạnh không
edge_check:

right:	
	bne	$s2, 1, left # Nếu bóng di chuyển sang trái, không kiểm tra cạnh phải, kiểm tra cạnh trái
	j	check_right

left:
	bne	$s2, -1, down
	j	check_left
	
down:
	bne	$s3, 1, up
	j	check_down
	
up:
	bne	$s3, -1, move_circle
	j	check_up

move_circle:
	add	$s5, $0, $0	# Đặt màu thành đen
	jal	draw_circle	# Xóa hình tròn cũ
	
	add	$s0, $s0, $s2 	# Cập nhật giá trị mới cho điểm trung tâm của hình tròn
	add	$s1, $s1, $s3 
	li	$s5, YELLOW	# Đặt màu thành vàng
	jal	draw_circle	# Vẽ hình tròn mới

loop:
	li $v0, 32	 	# Giá trị syscall để ngủ
	syscall
	j	input		# Làm mới chu kỳ

# Thủ tục bên dưới

circle_data:
	addi	$sp, $sp, -4	# Lưu $ra
	sw 	$ra, 0($sp)
	la 	$s5, circle	# $s5 trở thành con trỏ của mảng "circle"
	mul	$a3, $s4, $s4	# $a3 = r^2	
	add	$s7, $0, $0	# pixel x (px) = 0
	
pixel_data_loop:
	bgt	$s7, $s4, data_end
	mul	$t0, $s7, $s7	# $t0 = px^2
	sub	$a2, $a3, $t0	# $a2 = r^2 - px^2 = py^2
	jal	root		# $a2 = py
	
	add	$a1, $0, $s7	# $a1 = px
	add	$s6, $0, $0	# Sau khi lưu (px, py), (-px, py), (-px, -py), (px, -py), hoán đổi px và py, sau đó lưu (-py, px), (py, px), (py, -px), (-py, -px)
	
symmetric:
	beq	$s6, 2, finish
	jal	pixel_save	# px, py >= 0
	sub	$a1, $0, $a1	
	jal	pixel_save	# px <= 0, py >= 0
	sub	$a2, $0, $a2 
	jal	pixel_save	# px, py <= 0
	sub	$a1, $0, $a1	
	jal	pixel_save	# px >= 0, py <= 0
	
	add	$t0, $0, $a1	# Hoán đổi px và -py
	add	$a1, $0, $a2
	add	$a2, $0, $t0
	addi	$s6, $s6, 1
	j	symmetric

finish:	
	addi	$s7, $s7, 1 #px = px + 1
	j	pixel_data_loop
	
data_end:
	la	$t0, circle_end	
	sw	$s5, 0($t0)	# Lưu địa chỉ cuối của mảng "circle"
	lw	$ra, 0($sp)
	addi	$sp, $sp, 4
	jr	$ra
	
root:				# Tìm căn bậc hai của $a2
	add	$t0, $0, $0	# Đặt $t0 = 0
	add	$t1, $0, $0	# $t1 = $t0^2

root_loop:				
	beq	$t0, $s4, root_end	# Nếu $t0 vượt quá 20, 20 sẽ là căn bậc hai
	addi	$t2, $t0, 1		# $t2 = $t0 + 1
	mul	$t2, $t2, $t2		# $t2 = ($t0 + 1)^2
	sub	$t3, $a2, $t1		# $t3 = $a2 - $t0^2
	bgez	$t3, continue		# Nếu $t3 < 0, $t3 = -$t3
	sub	$t3, $0, $t3

continue:
	sub	$t4, $a2, $t2		# $t4 = $a2 - ($t0 + 1)^2
	bgez	$t4, compare		# Nếu $t4 < 0, $t4 = -$t4
	sub	$t4, $0, $t4

compare:
	blt	$t4, $t3, root_continue	# Nếu $t3 >= $t4, $t0 không gần căn bậc hai của $a2 hơn $t0 + 1
	add	$a2, $0, $t0		# Nếu không, $t0 là số gần nhất với căn bậc hai của $a2
	jr	$ra

root_continue:
	addi	$t0, $t0, 1
	add	$t1, $0, $t2
	j	root_loop

root_end:
	add	$a2, $0, $t0
	jr	$ra
	
pixel_save:
	sw	$a1, 0($s5)	# Lưu px trong mảng "circle"
	sw	$a2, 4($s5)	# Lưu py trong mảng "circle"
	addi	$s5, $s5, 8	# Di chuyển con trỏ đến khối null
	jr	$ra			
		
direction_change:
	li	$k0, KEY_CODE
	lw	$t0, 0($k0)

case_d:
	bne	$t0, 'd', case_a
	addi	$s2, $0, 1	# dx = 1
	add	$s3, $0, $0	# dy = 0
	jr	$ra

case_a:
	bne	$t0, 'a', case_s
	addi	$s2, $0, -1	# dx = -1	
	add	$s3, $0, $0	# dy = 0
	jr	$ra
	
case_s:
	bne	$t0, 's', case_w
	add	$s2, $0, $0	# dx = 0	
	addi	$s3, $0, 1	# dy = 1
	jr	$ra

case_w:
	bne	$t0, 'w', case_x
	add	$s2, $0, $0	# dx = 0	
	addi	$s3, $0, -1	# dy = -1
	jr	$ra

case_x:
	bne	$t0, 'x', case_z
	addi	$a0, $a0, 10	# t += 10
	jr	$ra
	
case_z:
	bne	$t0, 'z', default
	beq	$a0, 0, default	# Chỉ giảm t khi t >= 0 
	addi	$a0, $a0, -10	# t -= 10
		
default:
	jr	$ra

check_right:
	add	$t0, $s0, $s4	# Đặt $t0 sang phải của hình tròn (trung tâm + R)
	beq	$t0, 511, reverse_direction	# Đảo ngược hướng nếu cạnh chạm vào viền
	j	move_circle	# Quay lại nếu không
	
check_left:
	sub	$t0, $s0, $s4	# Đặt $t0 sang trái của hình tròn
	beq	$t0, 1, reverse_direction	# Đảo ngược hướng nếu cạnh chạm vào viền
	j	move_circle	# Quay lại nếu không

check_down:
	add	$t0, $s1, $s4	# Đặt $t0 xuống dưới của hình tròn
	beq	$t0, 511, reverse_direction	# Đảo ngược hướng nếu cạnh chạm vào viền
	j	move_circle	# Quay lại nếu không
	
check_up:
	sub	$t0, $s1, $s4	# Đặt $t0 lên trên của hình tròn
	beq	$t0, 1, reverse_direction	# Đảo ngược hướng nếu cạnh chạm vào viền
	j	move_circle
	
reverse_direction:
	sub	$s2, $0, $s2	# dx = -dx
	sub	$s3, $0, $s3	# dy = -dy
	j	move_circle

draw_circle:
	addi	$sp, $sp, -4	# Lưu $ra 
	sw 	$ra, 0($sp)
	la	$s6, circle_end	
	lw	$s7, 0($s6)	# $s7 trở thành địa chỉ cuối của mảng "circle"
	la	$s6, circle	# $s6 trở thành con trỏ đến mảng "circle"
	
draw_loop:
	beq	$s6, $s7, draw_end	# Dừng lại khi $s6 = $s7
	lw	$a1, 0($s6)		# Lấy px
	lw	$a2, 4($s6)		# Lấy py
	jal	pixel_draw
	addi	$s6, $s6, 8		# Đến pixel tiếp theo
	j	draw_loop
	
draw_end:
	lw	$ra, 0($sp)
	addi	$sp, $sp, 4
	jr	$ra										
					
pixel_draw:
	li	$t0, SCREEN_MONITOR
	add	$t1, $s0, $a1		# x cuối cùng (fx) = x + px
	add	$t2, $s1, $a2		# fy = y + py
	sll	$t2, $t2, 9		# $t2 = fy * 512
	add	$t2, $t2, $t1		# $t2 += fx
	sll	$t2, $t2, 2		# $t2 *= 4
	add	$t0, $t0, $t2
	sw	$s5, 0($t0)
	jr	$ra
